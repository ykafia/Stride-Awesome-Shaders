namespace Stride.Awesome.Shaders
{
    shader VoronoiNoise2DInverted<float Scale, float MinDist> : Texturing, ComputeColor
    {
        float2 Random2( float2 p ) 
        {
            return frac(sin(float2(dot(p,float2(127.1,311.7)),dot(p,float2(269.5,183.3))))*43758.5453);
        }

        override float4 Compute() {
            float2 st = streams.TexCoord;
            float3 color = float3(0,0,0);


            // float2 st = gl_FragCoord.xy/u_resolution.xy;
            // st.x *= u_resolution.x/u_resolution.y;
            // float3 color = float3(0,0,0);

            // Scale
            st *= Scale;

            // Tile the space
            float2 i_st = floor(st);
            float2 f_st = frac(st);

            float m_dist = MinDist;  // minimum distance

            for (int y= -1; y <= 1; y++) {
                for (int x= -1; x <= 1; x++) {
                    // Neighbor place in the grid
                    float2 neighbor = float2(float(x),float(y));

                    // Random position from current + neighbor place in the grid
                    float2 pointV = Random2(i_st + neighbor);

                    // Animate the point
                    pointV = 0.5 + 0.5*sin(Global.Time + 6.2831*pointV);

                    // Vector between the pixel and the point
                    float2 diff = neighbor + pointV - f_st;

                    // Distance to the point
                    float dist = length(diff);

                    // Keep the closer distance
                    m_dist = min(m_dist, dist);
                }
            }

            // Draw the min distance (distance field)
            color += m_dist;

            // Draw cell center
            // color += 1.-step(.02, m_dist);

            // Draw grid
            color.r += step(.98, f_st.x) + step(.98, f_st.y);

            // Show isolines
            // color -= step(.7,abs(sin(27.0*m_dist)))*.5;

            return float4(float3(1,1,1)-color,1.0);
        }

    };
}